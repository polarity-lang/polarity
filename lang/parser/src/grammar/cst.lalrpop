use lalrpop_util::ErrorRecovery;
use num_bigint::BigUint;

use crate::cst::exp::*;
use crate::cst::decls::*;
use crate::cst::ident::*;
use crate::lexer::{Token, LexicalError, StringLit};

use super::util::span;

#[LALR]
grammar<'err>(errors: &'err mut Vec<ErrorRecovery<usize, Token, LexicalError>>);

// Tokens
extern {
  type Location = usize;
  type Error = LexicalError;

  enum Token {
    // Keywords
    //
    //
    "data" => Token::Data,
    "codata" => Token::Codata,
    "def" => Token::Def,
    "codef" => Token::Codef,
    "let" => Token::Let,
    "match" => Token::Match,
    "as" => Token::As,
    "comatch" => Token::Comatch,
    "absurd" => Token::Absurd,
    "implicit" => Token::Implicit,
    "use" => Token::Use,
    "infix" => Token::Infix,
    "note" => Token::Note,

    // Parens, Braces and Brackets
    //
    //
    "(" => Token::LParen,
    ")" => Token::RParen,
    "{" => Token::LBrace,
    "}" => Token::RBrace,
    "[" => Token::LBracket,
    "]" => Token::RBracket,

    // Symbols
    //
    //
    ";" => Token::Semicolon,
    ":=" => Token::ColonEq,
    "=>" => Token::DoubleRightArrow,
    "," => Token::Comma,
    ":" => Token::Colon,
    "." => Token::Dot,
    "?" => Token::QuestionMark,
    "\\" => Token::Backslash,
    "#" => Token::Hash,
    "_" => Token::Underscore,

    // Identifiers
    //
    //
    "Identifier" => Token::Ident(<String>),
    "Operator" => Token::Operator(<String>),

    // Literals
    //
    //
    "NumLit" => Token::NumLit(<BigUint>),
    "StringLit" => Token::StringLit(<StringLit>),

    // DocComments
    "DocComment" => Token::DocComment(<String>),
  }
}

// Utils
// Based in part on Gluons grammar (https://github.com/gluon-lang/gluon/blob/fac08dcff7a019440dd8d0a886abdb18baa58198/parser/src/grammar.lalrpop)

Comma<Rule>: Vec<Rule> =
    <rules: (<Rule> ",")*> <last: Rule?> => {
        let mut rules = rules;
        rules.extend(last);
        rules
    };

Parens<Rule>: Rule =
    "(" <rule: Rule> ")" => rule;

Braces<Rule>: Rule =
    "{" <rule: Rule> "}" => rule;

Brackets<Rule>: Rule =
    "[" <rule: Rule> "]" => rule;

ParenthesizedArgs<Rule>: Vec<Rule> = Parens<Comma<Rule>>;
OptParenthesizedArgs<Rule>: Vec<Rule> = <args: Parens<Comma<Rule>>?> => args.unwrap_or_default();

BracedArgs<Rule>: Vec<Rule> = Braces<Comma<Rule>>;
OptBracedArgs<Rule>: Vec<Rule> = <args: Braces<Comma<Rule>>?> => args.unwrap_or_default();

BracketedArgs<Rule>: Vec<Rule> = Brackets<Comma<Rule>>;
OptBracketedArgs<Rule>: Vec<Rule> = <args: Brackets<Comma<Rule>>?> => args.unwrap_or_default();

Param: Param = <implicit: "implicit"?> <name: BindingSite> <names: BindingSite*> ":" <typ: Exp> => Param { implicit: implicit.is_some(), name, names, typ } ;

Params: Vec<Param> = ParenthesizedArgs<Param>;
OptParams: Vec<Param> = OptParenthesizedArgs<Param>;

Telescope: Telescope = <params: Params> => Telescope(params);
OptTelescope: Telescope = <params: OptParams> => Telescope(params);


OptTelescopeInst: Vec<BindingSite> = <params: Parens<Comma<BindingSite>>?> => params.unwrap_or_default();

Args: Vec<Arg> = ParenthesizedArgs<Arg>;
OptArgs: Vec<Arg> = OptParenthesizedArgs<Arg>;

Attr: String = <s:"Identifier"> => s.to_owned();
Attributes: Attributes = "#" <attrs: BracketedArgs<Attr>> => Attributes { attrs };
OptAttributes: Attributes = <attr: Attributes? > => attr.unwrap_or_default();

DocCommentHelper: Vec<String> = <doc: "DocComment"> => doc.strip_prefix("///").unwrap().lines().map(|s| s.trim().to_owned()).collect();
DocComment: DocComment = <docs: DocCommentHelper+> => DocComment { docs: docs.into_iter().flatten().collect() };

Arg: Arg = {
  <e: Exp> => Arg::UnnamedArg(e),
  <i: Ident> ":=" <e: Exp> => Arg::NamedArg(i,e),
}

// Modules
//
//

pub ModuleContents: (Vec<UseDecl>, Vec<Decl>) = {
    <use_decls: UseDecl*> <decls: Decls> => (use_decls, decls)
}

UseDecl: UseDecl = {
  <l: @L> "use" <path: "StringLit"> <r: @R> => UseDecl { span: span(l,r), path: path.unescaped },
}

Decls: Vec<Decl> = {
    <items: Decl*> => items,
}

Decl: Decl = {
    <d: Data> => Decl::Data(d),
    <d: Codata> => Decl::Codata(d),
    <d: Def> => Decl::Def(d),
    <d: Codef> => Decl::Codef(d),
    <d: Let> => Decl::Let(d),
    <d: Infix> => Decl::Infix(d),
    <d: Note> => Decl::Note(d),
}

// Data Type Declarations
//
//

Ctor: Ctor = {
    <l: @L> <doc: DocComment?> <name: Ident> <params: OptTelescope> <typ: (":" <TypApp>)?> <r: @R> => Ctor { span: span(l, r), doc, name, params, typ },
}

// Toplevel data type declaration
Data: Data = <l: @L> <doc: DocComment?> <attr: OptAttributes> "data" <name: Ident> <params: OptTelescope> "{" <ctors: Comma<Ctor>> "}" <r: @R> =>
  Data { span: span(l, r), doc, name, attr, params, ctors };


// Codata Type Declarations
//
//

pub Destructee: Destructee = {
    <l: @L> <scrutinee: (<Scrutinee>)?> <r: @R> => match scrutinee {
        Some(Scrutinee { span, name, typ }) => Destructee { span, name, typ: Some(typ) },
        None => Destructee { span: span(l, r), name: None, typ: None },
    }
}

Dtor: Dtor = {
    <l: @L> <doc: DocComment?> <destructee: Destructee> "." <name: Ident> <params: OptTelescope> ":" <ret_typ: Exp> <r: @R> =>
      Dtor { span: span(l, r), doc, name, params, destructee, ret_typ },
}

// Toplevel codata type declaration
Codata: Codata = <l: @L> <doc: DocComment?> <attr: OptAttributes> "codata" <name: Ident> <params: OptTelescope> "{" <dtors: Comma<Dtor>> "}" <r: @R> =>
  Codata { span: span(l, r), doc, name, attr, params, dtors };


// Toplevel definition
//
//
Def: Def = <l: @L> <doc: DocComment?> <attr: OptAttributes> "def" <scrutinee: Scrutinee> "." <name: Ident> <params: OptTelescope> ":" <ret_typ: Exp> "{" <cases: Comma<Case<Pattern>>> "}" <r: @R> =>
  Def { span: span(l, r), doc, name, attr, params, scrutinee, ret_typ, cases };

// Toplevel codefinition
Codef: Codef = <l: @L> <doc: DocComment?> <attr: OptAttributes> "codef" <name: Ident> <params: OptTelescope> ":" <typ: TypApp> "{" <cases: Comma<Case<Copattern>>> "}" <r: @R> =>
  Codef { span: span(l, r), doc, name, attr, params, typ, cases };

// Toplevel let binding
Let: Let = <l: @L> <doc: DocComment?> <attr: OptAttributes> "let" <name: Ident><params: OptTelescope> ":" <typ: Exp> "{" <body: Exp> "}" <r: @R> =>
  Let { span: span(l,r), doc, name, attr, params, typ, body };

// Infix operator declaration
Infix: Infix = {
  <l: @L> <doc: DocComment?> <attr: OptAttributes> "infix" <pattern: BinOp> ":=" <rhs: CallWithArgs> <r: @R> => Infix { span: span(l,r), doc, attr, pattern, rhs }
}

// Toplevel note declaration
Note: Note = {
  <l: @L> <doc: DocComment?> <attr: OptAttributes> "note" <name: Ident> <r: @R> => Note { span: span(l, r), doc, name, attr }
}

Pattern: Pattern = {
  <l: @L><name: Ident><params: OptTelescopeInst><r: @R> => Pattern { span: span(l,r), name, params },
}

Copattern: Copattern = {
  <l: @L> "." <name: Ident><params: OptTelescopeInst> <r: @R> => Copattern { span: span(l,r), name, params },
}

// In the syntactic sugar `\ap(x) => e` we do not need to parse the `.` in front of the identifier.
CopatternLam: Copattern = {
  <l: @L> <name: Ident><params: OptTelescopeInst> <r: @R> => Copattern { span: span(l,r), name, params },
}

Case<P> : Case<P> = {
    <l: @L> <pattern: P> <body: AbsurdOrBody> <r: @R> => Case { span: span(l, r), pattern, body },
}

AbsurdOrBody: Option<Box<Exp>> = {
    "absurd" => None,
    "=>" <body: Exp> => Some(body),
}


pub Scrutinee: Scrutinee = {
    <l: @L> "(" <name: BindingSite> ":" <typ: TypApp> ")" <r: @R> => Scrutinee { span: span(l, r), name: match name { BindingSite::Wildcard{..} => None, BindingSite::Var { name,.. } => Some(name) }, typ },
    <l: @L> <typ: TypApp> <r: @R> => Scrutinee { span: span(l, r), name: None, typ },
}

pub TypApp: Call = {
    <l: @L> <name: Ident> <args: OptArgs> <r: @R> => Call { span: span(l, r), name, args },
}

// Expressions
//
//
// Grammar:
//
// ```text
// <Exp>    ::= <NonLet>
//           | let x : NonLet := NonLet ; Exp
// <NonLet> ::= <Ops>
//           | <Ops> : <Exp>
//           | <Ops> + <Ops>
//           | \<pat> => <Exp>
// <Ops>    ::= <Atom>
//           | <Ops>.f(<Exp>,...,<Exp>)
//           | <Ops>.match <Ident> as <Ident> => <Exp> { <pat> => <Exp>, ... }
// <Atom>   ::= (<Exp>)
//           | f(<Exp>,...,<Exp>)
//           | comatch <Ident> { <copat> => <Exp>, ... }
//           | _
//           | ?
//           | f
//           | n      (Literal)
// ```

pub Exp: Box<Exp> = {
    <e: LocalLet> => Box::new(Exp::LocalLet(e)),
    NonLet,

    // error recovery for invalid expressions
    <l:@L> <err: !> <r: @R> => { errors.push(err); Box::new(Exp::Error(span(l, r))) }
}

pub NonLet: Box<Exp> = {
    <e: Anno> => Box::new(Exp::Anno(e)),
    <e: BinOp> => Box::new(Exp::BinOp(e)),
    <e: Lam> => Box::new(Exp::Lam(e)),
    Ops,
}

pub Ops: Box<Exp> = {
    <e: DotCall> => Box::new(Exp::DotCall(e)),
    <e: LocalMatch> => Box::new(Exp::LocalMatch(e)),
    Atom,
}

pub Atom: Box<Exp> = {
    <e: CallWithArgs> => Box::new(Exp::Call(e)),
    <e: LocalComatch> => Box::new(Exp::LocalComatch(e)),
    <e: Hole> => Box::new(Exp::Hole(e)),
    <e: NatLit> => Box::new(Exp::NatLit(e)),
    <e: ParensExp> => Box::new(Exp::Parens(e)),
    <e: CallWithoutArgs> => Box::new(Exp::Call(e)),
}

// Constituent parts of expressions
//
//

Anno: Anno = <l: @L> <exp: Ops> ":" <typ: Exp> <r: @R> =>
  Anno { span: span(l, r), exp, typ };

BinOps: (Operator, Exp) = <operator: Operator> <rhs: Ops> => (operator, *rhs);
BinOp: BinOp = <l: @L> <lhs: Ops> <rhs: BinOps+> <r: @R> =>
  BinOp { span: span(l, r), lhs, rhs };

Lam: Lam = <l: @L> "\\" <case: Case<CopatternLam>> <r: @R> =>
  Lam { span: span(l, r), case };

DotCall: DotCall = <l: @L> <exp: Ops> "." <name: Ident> <args: OptArgs> <r: @R> =>
  DotCall { span: span(l, r), exp, name, args };

LocalMatch: LocalMatch = <l: @L> <on_exp: Ops> "." "match" <name: Ident?> <motive: Motive?> "{" <cases: Comma<Case<Pattern>>> "}" <r: @R> =>
  LocalMatch { span: span(l, r), name, on_exp, motive, cases };

CallWithArgs: Call = <l: @L> <name: Ident> <args: Args> <r: @R> =>
  Call { span: span(l, r), name, args };

CallWithoutArgs: Call = <l: @L> <name: Ident> <r: @R> =>
  Call { span: span(l, r), name, args: vec![] };

LocalComatch: LocalComatch = <l: @L> "comatch" <name: Ident?> "{" <cases: Comma<Case<Copattern>>> "}" <r: @R> =>
  LocalComatch { span: span(l, r), name, is_lambda_sugar: false, cases };

Hole: Hole = {
  <l: @L> "_" <r: @R> => Hole { span: span(l, r), kind: HoleKind::MustSolve },
  <l: @L> "?" <r: @R> => Hole { span: span(l, r), kind: HoleKind::CanSolve },
}

NatLit: NatLit = <l: @L> <n: "NumLit"> <r: @R> =>
  NatLit { span: span(l, r), val: n };

LocalLet: LocalLet = <l: @L> "let" <name: BindingSite> <typ: (":" <NonLet>)?> ":=" <bound: NonLet> ";" <body: Exp> <r: @R> =>
  LocalLet { span: span(l,r), name, typ, bound, body };

ParensExp: Parens = <l: @L> "(" <exp: Exp> ")" <r: @R> =>
  Parens { span: span(l,r), exp };

// Helpers
//
//

Motive: Motive = {
    <l: @L> "as" <param: BindingSite> "=>" <ret_typ: Exp> <r: @R> => Motive { span: span(l, r), param, ret_typ },
}

// Names

BindingSite: BindingSite = {
    <l: @L> <i: Ident> <r: @R> => BindingSite::Var { span: span(l,r), name: i },
    <l: @L> "_" <r: @R> => BindingSite::Wildcard { span: span(l,r) },
}

Ident: Ident = {
   <l: @L> <i: "Identifier"> <r: @R> => Ident { span: span(l,r), id: i.to_owned() }
}

Operator: Operator = {
  <l: @L> <i: "Operator"> <r: @R> => Operator { span: span(l,r), id: i.to_owned() }
}

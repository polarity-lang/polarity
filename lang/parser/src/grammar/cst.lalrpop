use std::rc::Rc;

use num_bigint::BigUint;

use crate::cst::exp::*;
use crate::cst::decls::*;

use super::util::span;

#[LALR]
grammar;

// Tokens
match {
    // Symbols
    "(", ")", "{", "}", "[", "]", ";", ":=", "=>", ",", ":", ".", "?",
    "->", "\\", "#",
    // Names
    "_",
    r"[a-zÎ±-Ï‰][a-zÎ±-Ï‰A-ZÎ‘-Î©0-9_]*['âºâ»â‚€â‚â‚‚â‚ƒâ‚„â‚…â‚†â‚‡â‚ˆâ‚‰â‚Šâ‚‹]*",
    r"[A-ZÎ‘-Î©ð”¹â„•ð•âˆƒâˆ€Ã—][a-zÎ±-Ï‰A-ZÎ‘-Î©0-9_]*['âºâ»â‚€â‚â‚‚â‚ƒâ‚„â‚…â‚†â‚‡â‚ˆâ‚‰â‚Šâ‚‹]*",
    // Literals
    r"0|[1-9][0-9]*",
    // Keywords
    "data", "codata",
    "def", "codef", "let",
    "match", "as", "comatch",
    "absurd", "..absurd",
    "Type",
    // Comments and whitespace
    r"\s*" => { }, // Skip whitespace
    r"-- \|[^\n\r]*[\n\r]*", // Don't skip doc-comments
    r"--(([^ \n\r]| [^\|\n\r])[^\n\r]*)?[\n\r]*" => { }, // Skip `--` comments
}

// Utils
// Based in part on Gluons grammar (https://github.com/gluon-lang/gluon/blob/fac08dcff7a019440dd8d0a886abdb18baa58198/parser/src/grammar.lalrpop)

Comma<Rule>: Vec<Rule> =
    <rules: (<Rule> ",")*> <last: Rule?> => {
        let mut rules = rules;
        rules.extend(last);
        rules
    };

Parens<Rule>: Rule =
    "(" <rule: Rule> ")" => rule;

Braces<Rule>: Rule =
    "{" <rule: Rule> "}" => rule;

Brackets<Rule>: Rule =
    "[" <rule: Rule> "]" => rule;

ParenthesizedArgs<Rule>: Vec<Rule> = Parens<Comma<Rule>>;
OptParenthesizedArgs<Rule>: Vec<Rule> = <args: Parens<Comma<Rule>>?> => args.unwrap_or_default();

BracedArgs<Rule>: Vec<Rule> = Braces<Comma<Rule>>;
OptBracedArgs<Rule>: Vec<Rule> = <args: Braces<Comma<Rule>>?> => args.unwrap_or_default();

BracketedArgs<Rule>: Vec<Rule> = Brackets<Comma<Rule>>;
OptBracketedArgs<Rule>: Vec<Rule> = <args: Brackets<Comma<Rule>>?> => args.unwrap_or_default();

Param: Param = <name: BindingSite> <names: BindingSite*> ":" <typ: Exp> => Param { name, names, typ } ;

ParamInst: ParamInst = <l: @L> <name: BindingSite> <r: @R> => ParamInst { span: span(l, r), name };

Params: Vec<Param> = ParenthesizedArgs<Param>;
OptParams: Vec<Param> = OptParenthesizedArgs<Param>;

Telescope: Telescope = <params: Params> => Telescope(params);
OptTelescope: Telescope = <params: OptParams> => Telescope(params);

TelescopeInst: TelescopeInst = <params: Parens<Comma<ParamInst>>> => TelescopeInst(params);
OptTelescopeInst: TelescopeInst = <params: Parens<Comma<ParamInst>>?> => TelescopeInst(params.unwrap_or_default());

Args: Vec<Rc<Exp>> = ParenthesizedArgs<Exp>;
OptArgs: Vec<Rc<Exp>> = OptParenthesizedArgs<Exp>;

Attribute: Attribute = "#" <attrs: BracketedArgs<LowerIdent>> => Attribute { attrs };
OptAttribute: Attribute = <attr: Attribute? > => attr.unwrap_or_default();

OmitAbsurd: bool = <absurd: "..absurd"?> => absurd.is_some();

DocCommentHelper: String = <doc: r"-- \|[^\n\r]*[\n\r]*"> => doc.strip_prefix("-- |").unwrap().trim().to_owned();
DocComment: DocComment = <docs: DocCommentHelper+> => DocComment { docs };

// Modules
//
//

pub Prg: Prg = {
    <items: Decl*> => Prg { items,  },
}

pub Decl: Decl = {
    <d: Data> => Decl::Data(d),
    <d: Codata> => Decl::Codata(d),
    <d: Def> => Decl::Def(d),
    <d: Codef> => Decl::Codef(d),
    <d: Let> => Decl::Let(d),
}

// Toplevel declarations
//
//

Ctor: Ctor = {
    <l: @L> <doc: DocComment?> <name: UpperIdent> <params: OptTelescope> <typ: (":" <TypApp>)?> <r: @R> => Ctor { span: span(l, r), doc, name, params, typ },
}

// Toplevel data type declaration
Data: Data = <l: @L> <doc: DocComment?> <attr: OptAttribute> "data" <name: UpperIdent> <params: OptTelescope> "{" <ctors: Comma<Ctor>> "}" <r: @R> =>
  Data { span: span(l, r), doc, name, attr, params, ctors };

Dtor: Dtor = {
    <l: @L> <doc: DocComment?> <destructee: Destructee> <name: LowerIdent> <params: OptTelescope> ":" <ret_typ: Exp> <r: @R> => Dtor { span: span(l, r), doc, name, params, destructee, ret_typ },
}

// Toplevel codata type declaration
Codata: Codata = <l: @L> <doc: DocComment?> <attr: OptAttribute> "codata" <name: UpperIdent> <params: OptTelescope> "{" <dtors: Comma<Dtor>> "}" <r: @R> =>
  Codata { span: span(l, r), doc, name, attr, params, dtors };

// Toplevel definition
Def: Def = <l: @L> <doc: DocComment?> <attr: OptAttribute> "def" <scrutinee: Scrutinee> "." <name: LowerIdent> <params: OptTelescope> ":" <ret_typ: Exp> "{" <body: Match> "}" <r: @R> =>
  Def { span: span(l, r), doc, name, attr, params, scrutinee, ret_typ, body };

// Toplevel codefinition
Codef: Codef = <l: @L> <doc: DocComment?> <attr: OptAttribute> "codef" <name: UpperIdent> <params: OptTelescope> ":" <typ: TypApp> "{" <body: Match> "}" <r: @R> =>
  Codef { span: span(l, r), doc, name, attr, params, typ, body };

// Toplevel let binding
Let: Let = <l: @L> <doc: DocComment?> <attr: OptAttribute> "let" <name: LowerIdent><params: OptTelescope> ":" <typ: Exp> "{" <body: Exp> "}" <r: @R> =>
  Let { span: span(l,r), doc, name, attr, params, typ, body };

pub Match : Match = {
    <l: @L> <cases: Comma<Case>> <omit_absurd: OmitAbsurd> <r: @R> => Match { span: span(l, r), cases, omit_absurd },
}

pub Case : Case = {
    <l: @L> <name: Ident> <args: OptTelescopeInst> <body: AbsurdOrBody> <r: @R> => Case { span: span(l, r), name, args, body },
}

pub AbsurdOrBody: Option<Rc<Exp>> = {
    "absurd" => None,
    "=>" <body: Exp> => Some(body),
}

pub Destructee: Destructee = {
    <l: @L> <scrutinee: (<Scrutinee> ".")?> <r: @R> => match scrutinee {
        Some(Scrutinee { span, name, typ }) => Destructee { span, name, typ: Some(typ) },
        None => Destructee { span: span(l, r), name: None, typ: None },
    }
}

pub Scrutinee: Scrutinee = {
    <l: @L> "(" <name: BindingSite> ":" <typ: TypApp> ")" <r: @R> => Scrutinee { span: span(l, r), name: match name { BindingSite::Wildcard => None, BindingSite::Var { name } => Some(name) }, typ },
    <l: @L> <typ: TypApp> <r: @R> => Scrutinee { span: span(l, r), name: None, typ },
}

pub TypApp: TypApp = {
    <l: @L> <name: UpperIdent> <args: OptArgs> <r: @R> => TypApp { span: span(l, r), name, args },
}

// Expressions
//
//

pub Exp: Rc<Exp> = {
    <e: Anno> => Rc::new(Exp::Anno(e)),
    <e: Fun> => Rc::new(Exp::Fun(e)),
    <e: Lam> => Rc::new(Exp::Lam(e)),
    Ops,
}

pub Ops = {
    <e: DotCall> => Rc::new(Exp::DotCall(e)),
    <e: LocalMatch> => Rc::new(Exp::LocalMatch(e)),
    App,
}

pub App = {
    <e: CallWithArgs> => Rc::new(Exp::Call(e)),
    <e: LocalComatch> => Rc::new(Exp::LocalComatch(e)),
    Builtins,
}

pub Builtins: Rc<Exp> = {
    <e: TypeUniv> => Rc::new(Exp::Type(e)),
    Holes,
}

pub Holes: Rc<Exp> = {
    <e: Hole> => Rc::new(Exp::Hole(e)),
    Atom,
}

pub Atom: Rc<Exp> = {
    <e: NatLit> => Rc::new(Exp::NatLit(e)),
    "(" <exp: Exp> ")" => exp,
    <e: CallWithoutArgs> => Rc::new(Exp::Call(e)),
}

// Constituent parts of expressions
//
//

Anno: Anno = <l: @L> <exp: Ops> ":" <typ: Exp> <r: @R> =>
  Anno { span: span(l, r), exp, typ };

Fun: Fun = <l: @L> <from: Ops> "->" <to: Exp> <r: @R> =>
  Fun { span: span(l, r), from, to };

Lam: Lam = <l: @L> "\\" <var: ParamInst> "." <body: Exp> <r: @R> =>
  Lam { span: span(l, r), var, body };

DotCall: DotCall = <l: @L> <exp: Ops> "." <name: Ident> <args: OptArgs> <r: @R> =>
  DotCall { span: span(l, r), exp, name, args };

LocalMatch: LocalMatch = <l: @L> <on_exp: Ops> "." "match" <name: Ident?> <motive: Motive?> "{" <body: Match> "}" <r: @R> =>
  LocalMatch { span: span(l, r), name, on_exp, motive, body };

CallWithArgs: Call = <l: @L> <name: Ident> <args: Args> <r: @R> =>
  Call { span: span(l, r), name, args };

CallWithoutArgs: Call = <l: @L> <name: Ident> <r: @R> =>
  Call { span: span(l, r), name, args: Args::new() };

LocalComatch: LocalComatch = <l: @L> "comatch" <name: Ident?> "{" <body: Match> "}" <r: @R> =>
  LocalComatch { span: span(l, r), name, is_lambda_sugar: false, body };

TypeUniv: Type = <l: @L> "Type" <r: @R> =>
  Type { span: span(l, r) };

Hole: Hole = <l: @L> "?" <r: @R> =>
  Hole { span: span(l, r) };

NatLit: NatLit = <l: @L> <s: r"0|[1-9][0-9]*"> <r: @R> =>
  NatLit { span: span(l, r), val: BigUint::parse_bytes(s.as_ref(), 10).unwrap() };

// Helpers
//
//

Motive: Motive = {
    <l: @L> "as" <param: ParamInst> "=>" <ret_typ: Exp> <r: @R> => Motive { span: span(l, r), param, ret_typ },
}

// Names

BindingSite: BindingSite = {
    <i: Ident> => BindingSite::Var { name: i },
    "_" => BindingSite::Wildcard,
}

Ident: String = {
    <s: LowerIdent> => s.to_owned(),
    <s: UpperIdent> => s.to_owned(),
}
LowerIdent: String = <s:r"[a-zÎ±-Ï‰][a-zÎ±-Ï‰A-ZÎ‘-Î©0-9_]*['âºâ»â‚€â‚â‚‚â‚ƒâ‚„â‚…â‚†â‚‡â‚ˆâ‚‰â‚Šâ‚‹]*"> => s.to_owned();
UpperIdent: String = <s:r"[A-ZÎ‘-Î©ð”¹â„•ð•âˆƒâˆ€Ã—][a-zÎ±-Ï‰A-ZÎ‘-Î©0-9_]*['âºâ»â‚€â‚â‚‚â‚ƒâ‚„â‚…â‚†â‚‡â‚ˆâ‚‰â‚Šâ‚‹]*"> => s.to_owned();

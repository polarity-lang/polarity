use std::rc::Rc;

use num_bigint::BigUint;

use crate::cst::exp::*;
use crate::cst::decls::*;
use crate::cst::ident::*;
use crate::lexer::{Token, LexicalError};

use super::util::span;

#[LALR]
grammar;

// Tokens
extern {
  type Location = usize;
  type Error = LexicalError;

  enum Token {
    // Keywords
    //
    //
    "data" => Token::Data,
    "codata" => Token::Codata,
    "def" => Token::Def,
    "codef" => Token::Codef,
    "let" => Token::Let,
    "match" => Token::Match,
    "as" => Token::As,
    "comatch" => Token::Comatch,
    "absurd" => Token::Absurd,
    "..absurd" => Token::DotsAbsurd,
    "Type" => Token::Type,
    "implicit" => Token::Implicit,

    // Parens, Braces and Brackets
    //
    //
    "(" => Token::LParen,
    ")" => Token::RParen,
    "{" => Token::LBrace,
    "}" => Token::RBrace,
    "[" => Token::LBracket,
    "]" => Token::RBracket,

    // Symbols
    //
    //
    ";" => Token::Semicolon,
    ":=" => Token::ColonEq,
    "=>" => Token::DoubleRightArrow,
    "," => Token::Comma,
    ":" => Token::Colon,
    "." => Token::Dot,
    "?" => Token::QuestionMark,
    "->" => Token::RightArrow,
    "\\" => Token::Backslash,
    "#" => Token::Hash,
    "_" => Token::Underscore,

    // Names
    //
    //
    "UpperCaseName" => Token::UpperCaseName(<String>),
    "LowerCaseName" => Token::LowerCaseName(<String>),

    // Literals
    //
    //
    "NumLit" => Token::NumLit(<BigUint>),

    // DocComments
    "DocComment" => Token::DocComment(<String>),
  }
}

// Utils
// Based in part on Gluons grammar (https://github.com/gluon-lang/gluon/blob/fac08dcff7a019440dd8d0a886abdb18baa58198/parser/src/grammar.lalrpop)

Comma<Rule>: Vec<Rule> =
    <rules: (<Rule> ",")*> <last: Rule?> => {
        let mut rules = rules;
        rules.extend(last);
        rules
    };

Parens<Rule>: Rule =
    "(" <rule: Rule> ")" => rule;

Braces<Rule>: Rule =
    "{" <rule: Rule> "}" => rule;

Brackets<Rule>: Rule =
    "[" <rule: Rule> "]" => rule;

ParenthesizedArgs<Rule>: Vec<Rule> = Parens<Comma<Rule>>;
OptParenthesizedArgs<Rule>: Vec<Rule> = <args: Parens<Comma<Rule>>?> => args.unwrap_or_default();

BracedArgs<Rule>: Vec<Rule> = Braces<Comma<Rule>>;
OptBracedArgs<Rule>: Vec<Rule> = <args: Braces<Comma<Rule>>?> => args.unwrap_or_default();

BracketedArgs<Rule>: Vec<Rule> = Brackets<Comma<Rule>>;
OptBracketedArgs<Rule>: Vec<Rule> = <args: Brackets<Comma<Rule>>?> => args.unwrap_or_default();

Param: Param = <implicit: "implicit"?> <name: BindingSite> <names: BindingSite*> ":" <typ: Exp> => Param { implicit: implicit.is_some(), name, names, typ } ;

Params: Vec<Param> = ParenthesizedArgs<Param>;
OptParams: Vec<Param> = OptParenthesizedArgs<Param>;

Telescope: Telescope = <params: Params> => Telescope(params);
OptTelescope: Telescope = <params: OptParams> => Telescope(params);


OptTelescopeInst: Vec<BindingSite> = <params: Parens<Comma<BindingSite>>?> => params.unwrap_or_default();

Args: Vec<Rc<Exp>> = ParenthesizedArgs<Exp>;
OptArgs: Vec<Rc<Exp>> = OptParenthesizedArgs<Exp>;

Attr: String = <s:"LowerCaseName"> => s.to_owned();
Attributes: Attributes = "#" <attrs: BracketedArgs<Attr>> => Attributes { attrs };
OptAttributes: Attributes = <attr: Attributes? > => attr.unwrap_or_default();

OmitAbsurd: bool = <absurd: "..absurd"?> => absurd.is_some();

DocCommentHelper: String = <doc: "DocComment"> => doc.strip_prefix("-- |").unwrap().trim().to_owned();
DocComment: DocComment = <docs: DocCommentHelper+> => DocComment { docs };

// Modules
//
//

pub Decls: Vec<Decl> = {
    <items: Decl*> => items,
}

pub Decl: Decl = {
    <d: Data> => Decl::Data(d),
    <d: Codata> => Decl::Codata(d),
    <d: Def> => Decl::Def(d),
    <d: Codef> => Decl::Codef(d),
    <d: Let> => Decl::Let(d),
}

// Toplevel declarations
//
//

Ctor: Ctor = {
    <l: @L> <doc: DocComment?> <name: UpperIdent> <params: OptTelescope> <typ: (":" <TypApp>)?> <r: @R> => Ctor { span: span(l, r), doc, name, params, typ },
}

// Toplevel data type declaration
Data: Data = <l: @L> <doc: DocComment?> <attr: OptAttributes> "data" <name: UpperIdent> <params: OptTelescope> "{" <ctors: Comma<Ctor>> "}" <r: @R> =>
  Data { span: span(l, r), doc, name, attr, params, ctors };

Dtor: Dtor = {
    <l: @L> <doc: DocComment?> <destructee: Destructee> <name: LowerIdent> <params: OptTelescope> ":" <ret_typ: Exp> <r: @R> => Dtor { span: span(l, r), doc, name, params, destructee, ret_typ },
}

// Toplevel codata type declaration
Codata: Codata = <l: @L> <doc: DocComment?> <attr: OptAttributes> "codata" <name: UpperIdent> <params: OptTelescope> "{" <dtors: Comma<Dtor>> "}" <r: @R> =>
  Codata { span: span(l, r), doc, name, attr, params, dtors };

// Toplevel definition
Def: Def = <l: @L> <doc: DocComment?> <attr: OptAttributes> "def" <scrutinee: Scrutinee> "." <name: LowerIdent> <params: OptTelescope> ":" <ret_typ: Exp> "{" <body: Match> "}" <r: @R> =>
  Def { span: span(l, r), doc, name, attr, params, scrutinee, ret_typ, cases: body.0, omit_absurd: body.1 };

// Toplevel codefinition
Codef: Codef = <l: @L> <doc: DocComment?> <attr: OptAttributes> "codef" <name: UpperIdent> <params: OptTelescope> ":" <typ: TypApp> "{" <body: Match> "}" <r: @R> =>
  Codef { span: span(l, r), doc, name, attr, params, typ, cases: body.0, omit_absurd: body.1 };

// Toplevel let binding
Let: Let = <l: @L> <doc: DocComment?> <attr: OptAttributes> "let" <name: LowerIdent><params: OptTelescope> ":" <typ: Exp> "{" <body: Exp> "}" <r: @R> =>
  Let { span: span(l,r), doc, name, attr, params, typ, body };

pub Match : (Vec<Case>, bool) = {
    <cases: Comma<Case>> <omit_absurd: OmitAbsurd> => (cases, omit_absurd),
}

pub Case : Case = {
    <l: @L> <name: Ident> <params: OptTelescopeInst> <body: AbsurdOrBody> <r: @R> => Case { span: span(l, r), name, params, body },
}

pub AbsurdOrBody: Option<Rc<Exp>> = {
    "absurd" => None,
    "=>" <body: Exp> => Some(body),
}

pub Destructee: Destructee = {
    <l: @L> <scrutinee: (<Scrutinee> ".")?> <r: @R> => match scrutinee {
        Some(Scrutinee { span, name, typ }) => Destructee { span, name, typ: Some(typ) },
        None => Destructee { span: span(l, r), name: None, typ: None },
    }
}

pub Scrutinee: Scrutinee = {
    <l: @L> "(" <name: BindingSite> ":" <typ: TypApp> ")" <r: @R> => Scrutinee { span: span(l, r), name: match name { BindingSite::Wildcard{..} => None, BindingSite::Var { name,.. } => Some(name) }, typ },
    <l: @L> <typ: TypApp> <r: @R> => Scrutinee { span: span(l, r), name: None, typ },
}

pub TypApp: TypApp = {
    <l: @L> <name: UpperIdent> <args: OptArgs> <r: @R> => TypApp { span: span(l, r), name, args },
}

// Expressions
//
//

pub Exp: Rc<Exp> = {
    <e: Anno> => Rc::new(Exp::Anno(e)),
    <e: Fun> => Rc::new(Exp::Fun(e)),
    <e: Lam> => Rc::new(Exp::Lam(e)),
    Ops,
}

pub Ops = {
    <e: DotCall> => Rc::new(Exp::DotCall(e)),
    <e: LocalMatch> => Rc::new(Exp::LocalMatch(e)),
    App,
}

pub App = {
    <e: CallWithArgs> => Rc::new(Exp::Call(e)),
    <e: LocalComatch> => Rc::new(Exp::LocalComatch(e)),
    Builtins,
}

pub Builtins: Rc<Exp> = {
    <e: TypeUniv> => Rc::new(Exp::TypeUniv(e)),
    Holes,
}

pub Holes: Rc<Exp> = {
    <e: Hole> => Rc::new(Exp::Hole(e)),
    Atom,
}

pub Atom: Rc<Exp> = {
    <e: NatLit> => Rc::new(Exp::NatLit(e)),
    "(" <exp: Exp> ")" => exp,
    <e: CallWithoutArgs> => Rc::new(Exp::Call(e)),
}

// Constituent parts of expressions
//
//

Anno: Anno = <l: @L> <exp: Ops> ":" <typ: Exp> <r: @R> =>
  Anno { span: span(l, r), exp, typ };

Fun: Fun = <l: @L> <from: Ops> "->" <to: Exp> <r: @R> =>
  Fun { span: span(l, r), from, to };

Lam: Lam = <l: @L> "\\" <var: BindingSite> "." <body: Exp> <r: @R> =>
  Lam { span: span(l, r), var, body };

DotCall: DotCall = <l: @L> <exp: Ops> "." <name: Ident> <args: OptArgs> <r: @R> =>
  DotCall { span: span(l, r), exp, name, args };

LocalMatch: LocalMatch = <l: @L> <on_exp: Ops> "." "match" <name: Ident?> <motive: Motive?> "{" <body: Match> "}" <r: @R> =>
  LocalMatch { span: span(l, r), name, on_exp, motive, cases: body.0, omit_absurd: body.1 };

CallWithArgs: Call = <l: @L> <name: Ident> <args: Args> <r: @R> =>
  Call { span: span(l, r), name, args };

CallWithoutArgs: Call = <l: @L> <name: Ident> <r: @R> =>
  Call { span: span(l, r), name, args: vec![] };

LocalComatch: LocalComatch = <l: @L> "comatch" <name: Ident?> "{" <body: Match> "}" <r: @R> =>
  LocalComatch { span: span(l, r), name, is_lambda_sugar: false, cases: body.0, omit_absurd: body.1 };

TypeUniv: TypeUniv = <l: @L> "Type" <r: @R> =>
  TypeUniv { span: span(l, r) };

Hole: Hole = <l: @L> "?" <r: @R> =>
  Hole { span: span(l, r) };

NatLit: NatLit = <l: @L> <n: "NumLit"> <r: @R> =>
  NatLit { span: span(l, r), val: n };

// Helpers
//
//

Motive: Motive = {
    <l: @L> "as" <param: BindingSite> "=>" <ret_typ: Exp> <r: @R> => Motive { span: span(l, r), param, ret_typ },
}

// Names

BindingSite: BindingSite = {
    <l: @L> <i: Ident> <r: @R> => BindingSite::Var { span: span(l,r), name: i },
    <l: @L> "_" <r: @R> => BindingSite::Wildcard { span: span(l,r) },
}

Ident: Ident = {
    <s: LowerIdent> => s.to_owned(),
    <s: UpperIdent> => s.to_owned(),
}
LowerIdent: Ident = <s: "LowerCaseName" > => Ident { id: s.to_owned() };
UpperIdent: Ident = <s: "UpperCaseName"> => Ident { id: s.to_owned() };
